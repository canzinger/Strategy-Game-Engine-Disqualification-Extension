plugins {
    id 'java'
    id 'idea'
    id "maven-publish"
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id "com.jfrog.bintray" version "1.8.5"
}

group 'at.ac.tuwien.ifs.sge'
version '1.0.1'

sourceCompatibility = 1.11

repositories {
    mavenCentral()
    jcenter()
}


dependencies {

    compile group: 'info.picocli', name: 'picocli', version: '3.9.6'
    //   compile 'org.jgrapht:jgrapht-core:1.3.0'
    compile group: 'org.fusesource.jansi', name: 'jansi', version: '1.18'
    compile group: 'com.google.guava', name: 'guava', version: '28.0-jre'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

bintray {
    user = "${bintrayName}"
    key = "${bintrayKey}"
    publications = ['mavenPublication']
    pkg {
        repo = 'StrategyGameEngine'
        name = project.getName()
        version {
            name = project.getVersion()
            released = new Date()
        }
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'at.ac.tuwien.ifs.sge.engine.cli.SgeCommand'
    }
}

shadowJar {
    archiveClassifier.set('exe')
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = "sge"
                description = "The core engine library, needed to implement games and agents."
                licenses {
                    license {
                        name = "GNU Affero General Public License version 3"
                        url = "https://opensource.org/licenses/agpl-v3.html"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        id = "entze"
                        name = "Lukas Grassauer"
                        email = "entze@grassauer.eu"
                    }
                }

                scm {
                    url = "https://gitlab.com/Entze/strategy-game-engine"
                    connection = "https://gitlab.com/Entze/strategy-game-engine.git"
                    developerConnection = "git@gitlab.com:Entze/strategy-game-engine.git"
                }
            }
            repositories {
                maven {
                    url = "https://jcenter.bintray.com/at/ac/tuwien/ifs/sge/sge/"
                }
            }
        }
    }
}
